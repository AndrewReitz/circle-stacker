buildscript {
  repositories {
    jcenter()
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.1.3'
    classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.6-SNAPSHOT'
  }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply from: rootProject.file('gradle/signing.gradle')
apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'groovyx.grooid.groovy-android'

repositories {
  jcenter()
}

dependencies {
  compile 'org.codehaus.groovy:groovy:2.4.1:grooid'

  compile 'com.android.support:support-v4:22.0.0'
  compile 'com.android.support:support-annotations:22.0.0'
  compile 'com.android.support:appcompat-v7:22.0.0'

  compile "com.arasthel:swissknife:1.2.2"

  compile 'io.reactivex:rxandroid:0.24.0'
  compile 'io.reactivex:rxjava:1.0.8'

  compile 'com.jakewharton.timber:timber:2.7.1'
}

android {
  compileSdkVersion 22
  buildToolsVersion "21.1.2"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 22

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
  }

  signingConfigs {
    release {
      storeFile file(storeFilePath)
      storePassword keystorePassword
      keyAlias storeKeyAlias
      keyPassword aliasKeyPassword
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.dev'
      versionNameSuffix '-dev'
    }

    release {
      signingConfig signingConfigs.release
    }
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

androidGroovy {
  options {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
  }
}

apply from: rootProject.file('gradle/checkstyle.gradle')
