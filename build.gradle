buildscript {
  repositories {
    jcenter()
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local' }
    maven { url 'https://maven.fabric.io/public' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.1.3'
    classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    classpath 'io.fabric.tools:gradle:1.18.0'
    classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.6'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.8'
  }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

apply from: rootProject.file('gradle/signing.gradle')
apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'groovyx.grooid.groovy-android'

repositories {
  jcenter()
  maven { url 'https://maven.fabric.io/public' }
}

dependencies {
  compile 'org.codehaus.groovy:groovy:2.4.3:grooid'

  compile 'com.android.support:support-v4:22.0.0'
  compile 'com.android.support:support-annotations:22.0.0'
  compile 'com.android.support:appcompat-v7:22.0.0'

  compile "com.arasthel:swissknife:1.2.2"

  compile 'io.reactivex:rxandroid:0.24.0'
  compile 'io.reactivex:rxjava:1.0.8'

  compile 'com.jakewharton.timber:timber:2.7.1'

  compile('com.crashlytics.sdk.android:crashlytics:2.2.2@aar') {
    transitive = true
  }
}

android {
  compileSdkVersion 22
  buildToolsVersion "21.1.2"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 22

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
  }

  signingConfigs {
    debug {
      storeFile file('debug.keystore')
    }
    release {
      storeFile file(storeFilePath)
      storePassword keystorePassword
      keyAlias storeKeyAlias
      keyPassword aliasKeyPassword
    }
  }

  buildTypes {
    debug {
      debuggable true
      applicationIdSuffix '.dev'
      versionNameSuffix '-dev'
      signingConfig signingConfigs.debug
    }

    release {
      minifyEnabled true
      debuggable false
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
    }
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  // Because the android plugin screws up the settings from the groovy plugin...
  sourceSets.debug.java.srcDirs += 'src/debug/groovy'
  sourceSets.release.java.srcDirs += 'src/release/groovy'
}

androidGroovy {
  options {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
  }
}
